//--------------------------------------------------------------------
// The Aurora Editor v3.0, Copyright (C) 1993-1996 nuText Systems
//
// KBD.AML
// Aurora Keyboard definitions (included by Main.aml)
//
// If you have made any changes, save this file and select Recompile
// the Editor from the Set menu. Exit and re-enter the editor for
// your changes to take effect.
//--------------------------------------------------------------------

//--------------------------------------------------------------------
// Edit and File Manager windows
//--------------------------------------------------------------------

object edit_fmgr

  // Controls
  function 'ð'                                 // close window
    close
  end

  // Menu activation
  key  <esc>           gotobar                 // to last menu bar item
  key  <f10>           gotobar                 // to last menu bar item
  key  <alt f10>       gotomenu                // goto last pull-down menu
  key  <f1>            gotomenu "help"         // to help menu
  key  <alt t>         gotobar2                // to last toolbar/drive item

  // Scroll
  key  <pgdn>          pagedown                // scroll down
  key  <pgup>          pageup                  // scroll up
  key  <ctrl home>     row (getviewtop)        // to page top
  key  <ctrl end>      row (getviewbot)        // to page bottom
  key  <ctrl pgup>     row 1                   // to file top
  key  <ctrl pgdn>     row (getlines)          // to file bottom
  key  <center>        adjustrow               // center cursor
  key  <ctrl f9>       adjustrow 1             // scroll to page top
  key  <ctrl f10>      adjustrow (getviewrows) // scroll to page bottom
  key  <ctrl up>       rollrow -1              // scroll up one line
  key  <ctrl down>     rollrow  1              // scroll down one line
  key  <ctrl ->        lastpos                 // to last cursor position

  // File
  key  <ctrl n>        opennew                 // new edit window
  key  <alt e>         askopen                 // open prompt
  key  <alt z>         openlast                // open last window
  key  <alt ->         filelist                // file list
  key  <alt q>         close                   // close window
  key  <alt x>         closeall                // close all windows
  key  <shift f1>      quickref 'qw'           // quick function reference
  key  <shift f2>      quickref 'fw'           // function reference

  // Window
  key  <alt w>         winlist                 // window list
  key  <ctrl z>        maximize                // maximize window
  key  <ctrl a>        nextwindow              // next window
  key  <f12>           prevwindow              // prev window
  key  <shift f3>      tile 'v'                // tile vertical
  key  <shift f4>      tile 'h'                // tile horizontal
  key  <shift f5>      cascade                 // cascade
  key  <ctrl k><q>     close                   // close window

  // Search
  key  <ctrl s>        askscan                 // file scan prompt
  key  <ctrl b>        gotomark 't'            // find top of block
  key  <ctrl 6>        cyclebook               // cycle through bookmarks

  // Print
  key  <ctrl p>        print                   // print
  key  <alt p>         print 'b'               // print block

  // Set
  key  <ctrl f1>       togglemode              // toggle video mode
  key  <alt f2>        recompile               // recompile the editor

  // Macro
  key  <ctrl v>        askeval                 // macro command prompt
  key  <alt f9>        askrun                  // Dos command prompt
  key  <alt f8>        askruncap               // Dos capture prompt
  key  <f9>            shell                   // exit to Dos
  key  <shift f12>     pickmacro               // macro picklist
  key  <ctrl f12>      runmacro2 "maclist"     // macro menu

  // Other
  key  <alt f5>        opencfg "tran"          // edit translation table

  // undefined or unnamed keys
  key <otherkey> (keycode)
    say (geteventname keycode) + " not defined"
  end


//--------------------------------------------------------------------
// Prompts and Edit windows
//--------------------------------------------------------------------

object prompt

  // Controls
  function 'ð'
    close                                      // close window
  end

  function '*'
    enter                                      // simulate <enter>
  end                                          //  (2-line box only)

  // Cursor
  key  <left>          left                    // move cursor left
  key  <right>         right                   // move cursor right
  key  <home>          col 1                   // to column one
  key  <end>           col  getlinelen + 1     // to end of line

  // Cursor + CUA-marking
  key  <shift left>    left
                       smark
  key  <shift right>   right
                       smark
  key  <shift home>    col 1
                       smark
  key  <shift end>     col getlinelen + 1
                       smark

  // Scroll
  key  <f7>            rollcol -(getviewcols - 1)   // page left
  key  <f8>            rollcol   getviewcols - 1    // page right

  // Editing
  key  <ins>           setting 'I' TOGGLE      // toggle insert mode
  key  <del>           delchar                 // delete character

  key  <backspace>     backsp                  // delete left character
  key  <f6>            delchar MAX_COL         // erase to end of line
  key  <alt f6>                                // erase to beginning of line
    ovltext '':getcol 1
  end
  key  <ctrl [>        literal                 // enter literal character
  key  <alt =>         asciilist               // display ascii chart

  // Block
  key  <alt a>         markchar                // mark character
  key  <alt b>         markcolumn              // mark column
  key  <alt 1>         markword                // mark word
  key  <alt 2>         markeol                 // mark to end of line
  key  <alt u>         destroymark             // unmark

  // copy block to prompt
  key  <alt c>
    instext (getmarktext)
    col  getlinelen + 1
  end

  // paste from clipboard to prompt
  key  <grey*>
    oldmark = usemark _ClipName
    instext (getmarktext)
    usemark oldmark
    col  getlinelen + 1
  end
  key  <shift ins>     call <grey*>

  // copy from prompt to clipboard
  key  <grey+>         copy
  key  <ctrl ins>      copy

  // Prompt history
  key  <up>            prevhist                // retrieve prev prompt
  key  <down>          nexthist                // retrieve next prompt
  key  <pgup>          askhistory              // history popup menu
  key  <pgdn>          askhistory              // history popup menu

  // Exit
  key  <esc>           close                   // quit prompt
  key  <alt q>         close                   // quit prompt

  // non-function keys
  key  <char> (character)                      // typeable keys
    write character
  end

  // filename completion
  key  <ctrl tab>      askcomplete             // filename completion
  key  <tab>           askcomplete             // filename completion


//--------------------------------------------------------------------
// Edit windows
//--------------------------------------------------------------------

object edit

  // Controls
  function 'ð'
    close                                      // close window
  end

  // Menu
  key  <esc>           gotobar                 // to last menu bar item

  // Cursor
  key  <up>            up                      // move cursor up
  key  <down>          down                    // move cursor down

  // Cursor + CUA-marking
  key  <shift up>      up
                       smark
  key  <shift down>    down
                       smark

  key  <enter>         enter                   // enter key
  key  <greyenter>     enter                   // keypad enter key
  key  <del>           delchar2                // delete character
  key  <backspace>     backsp                  // delete left char
  key  <ctrl t>        delword _CSet           // delete right word
  key  <tab>           tabright                // tab right
  key  <shift tab>     tableft                 // tab left

  key  <ctrl left>     prevword                // find left word
                       smark                   // cua marking
  key  <ctrl right>    nextword                // find right word
                       smark                   // cua marking

  // Scroll
  key  <pgdn>          pagedown                // page down
  key  <pgup>          pageup                  // page up

  // Scroll + CUA-marking
  key  <shift pgdn>    pagedown
                       smark
  key  <shift pgup>    pageup
                       smark

  key  <ctrl home>     row (getviewtop)        // to page top
                       smark
  key  <ctrl end>      row (getviewbot)        // to page bottom
                       smark

  key  <ctrl pgup>     row 1                   // to file top
                       smark
  key  <ctrl pgdn>     row (getlines)          // to file bottom
                       smark

  key  <ctrl up>       rollrow -1              // scroll up one line
                       smark
  key  <ctrl down>     rollrow  1              // scroll down one line
                       smark

  // File
  key  <alt i>         askinsert               // open and insert prompt
  key  <ctrl ]>        openword                // open file at cursor
  key  <alt n>         askname                 // rename prompt
  key  <f3>            save                    // save file
  key  <ctrl del>      nextfile                // next file
  key  <ctrl ins>      prevfile                // prev file
  key  <f4>            open "*.*"              // display file manager
  key  <alt q>         close                   // close file/window
  key  <ctrl x>        close 's'               // save & close file/window

  // Window
  key  <ctrl c>        copywin                 // copy window
  key  <alt h>         splitwin 'h'            // split window horizontal
  key  <alt v>         splitwin 'v'            // split window vertical
  key  <ctrl f8>       toolbar                 // display tool bar

  // Block
  key  <alt l>         markline                // mark line
  key  <alt 3>         markpara "tb"           // mark paragraph
  key  <alt c>         copyblock2              // copy block
  key  <alt o>         copyblockover           // overlay block
  key  <alt m>         moveblock2              // move block
  key  <ctrl m>        moveblockover           // move block over
  key  <alt d>         deleteblock2            // delete block
  key  <shift f7>      shiftblock -1           // unindent block
  key  <shift f8>      shiftblock  1           // indent block
  key  <ctrl k><f>     fillblock2              // fill block with string
  key  <alt r>         formatblock2            // reformat block
  key  <alt y>         formatblock2 "rj"       // reformat & right just block
  key  <ctrl k><s>     saveblock2              // save block
  key  <ctrl k><o>     sortblock2              // sort block
  key  <ctrl k><u>     caseblock               // change block to uppercase
  key  <shift f9>      quote                   // quote a block
  key  <ctrl k><c>     justblock2 'c'          // center a block

  // Search
  key  <f5>            askfind                 // find prompt
  key  <ctrl f>        askfind                 // find prompt
  key  <ctrl g>        askrepl                 // replace prompt
  key  <ctrl i>        isearch                 // incremental search
  key  <ctrl h>        askfindo                // find occurrences

  key  <ctrl l>        findlast                // do last find/replace
  key  <ctrl 2>        quickbook               // set quick bookmark
  key  <ctrl j>        askrow                  // go to line prompt
  key  <alt 7>         search2 "f/f"           // go to next fold
  key  <alt f3>        gotomatch2              // find matching char
  key  <alt f7>        gotoerror               // go to compiler error

  // Fold
  key  <alt 8>         foldline                // fold next line
  key  <alt 9>         foldline 'u'            // unfold next line
  key  <alt g>         destroyfold2            // destroy closed/open fold
  key  <alt f>         foldblock2              // fold block
  key  <alt \>                                 // open or close fold
    if fold? then
       openfold
    else
       closefold
    end
  end
  key  <alt 0>         foldall 'ds'            // destroy all folds
  key  <alt [>         foldall 'os'            // open all folds
  key  <alt ]>         foldall 'cs'            // close all folds

  // Edit
  key  <ctrl u>        undo                    // undo last change
  key  <ctrl y>        redo                    // redo last change
  key  <f2>            insabove                // insert line above
  key  <ctrl enter>    insline2                // insert line
  key  <ctrl backspace> delline                // delete line
  key  <alt s>         splitline2              // split line
  key  <alt j>         joinline                // join line
  key  <alt 4>         copyblock '*l'          // duplicate line
  key  <alt 5>         swapline                // swap line
  key  <alt 6>         centerline              // center line
  key  <alt f1>        commentline             // comment/uncomment line
  key  <ctrl k><t>     timestamp               // date/time stamp
  key  <ctrl k><x>     tabfile                 // expand tabs
  key  <ctrl \>        hiliteword              // highlight words

  // Clipboard
  key  <grey->         cut                     // cut
  key  <shift del>     cut                     // cut
  key  <ctrl grey->    cut 'a'                 // cut append
  key  <grey+>         copy                    // copy
  key  <ctrl grey+>    copy 'a'                // copy append
  key  <ctrl k><grey+> copy 'w'                // copy to Windows
  key  <grey*>         paste                   // paste
  key  <shift ins>     paste                   // paste
  key  <ctrl grey*>    paste 'o'               // paste over
  key  <ctrl k><grey*> paste 'w'               // paste from Windows
  key  <ctrl grey/>    clear                   // clear clipboard

  // Set
  key  <ctrl w>        setting 'L' TOGGLE      // live word wrap toggle
  key  <ctrl d>        setting 'D' TOGGLE      // line draw toggle
  key  <alt f4>        setting 'T' TOGGLE      // text translate toggle

  // Macro
  key  <shift f10>     compilemacro2 (getbufname)  // compile current file
  key  <shift f11>     runmacro2 (getbufname)      // run current file

  // non-function (typeable) keys
  key  <char> (character)                      // typeable keys
    write character
  end

  // Other
  key  <ctrl q><q>     askrepkey               // repeat entered keys

  // invoke a spell checker from within an edit window
  // (replace 'jspell' with your favorite spell checker)
  key  <f11>
    save                                       // save the current file
    run  "jspell " + getbufname  "ck"          // call spellchecker
    reopen                                     // reopen current file
  end

//--------------------------------------------------------------------
// File Manager windows
//--------------------------------------------------------------------

object fmgr

  // Menu activation
  key  <tab>           gotobar2                // to drive menu bar

  // Cursor
  key  <left>          rollcol -1              // scroll left one column
  key  <right>         rollcol  1              // scroll right one column
  key  <home>          col 1                   // scroll to column one

  key  <up>            up                      // move cursor up
  key  <down>          down                    // move cursor down

  key  <shift up>      fmark                   // mark files
                       up
  key  <shift down>    fmark                   // mark files
                       down


  // file manager commands (single character command codes)
  key  <char> (c)

    // toggle file mark
    if c == ' ' then
      fmark

    // <shift-character> commands
    elseif shiftkey? then
      case  locase c
        when 'o'     fopen 'o'                   // open file/directory
        when 'e'     fopen 'e'                   // open file/directory
        when 'z'     fopen "ze"                  // open maximized
        when 'b'     fopen 'b'                   // open binary file
        when 'y'     fopen "be"                  // open binary in one window
        when 'f'     flopen (fgetfile)           // open file list
        when 'k'     openkey2 (fgetfile)         // open key macro file
        when 'm'     fmove                       // move file
        when 'c'     fcopy                       // copy file
        when 'd'     fdelete                     // delete file
        when 'n'     frename                     // rename file
        when 'r'     frun 'c'                    // run program/batch file
        when 'p'     fprint                      // print file
        when 'l'     fremove                     // remove line
        when 't'     ftouch                      // touch file
        when 'a'     fattr                       // change file attributes
        when 'i'     askscan TRUE                // scan files

        // spell checker
        when 's'     run "jspell " + fgetfile  "ck"

        // unarchive .ZIP or .LZH files
        when 'u'
          f = fgetfile
          run (if? (pos ".lzh" f 'i') "lha e " "pkunzip ") + f  "ck"
          reopen

        // view .ZIP or .LZH archives
        when 'v'
          f = fgetfile
          runcap (if? (pos ".lzh" f 'i') "lha v " "pkunzip -v ") + f
      end

    // hotkey to files
    else
      onhotkey c
    end

  key  <backspace>     onhotkey '\x08'         // undo hotkey

  // File
  key  <ctrl backspace>   fup                  // parent directory

  // Mark
  key  <alt m>         fmark "ma"              // mark all files
  key  <alt u>         fmark "ua"              // unmark all

  // Command
  key  <enter>         fopen '1'               // open file (one only)
  key  <ctrl enter>    fopen 'q'               // open file (close fmgr)
  key  <del>           fdelete                 // delete file

  // Sort
  key  <alt n>         fsort 'n'               // sort by name
  key  <alt s>         fsort 's'               // sort by size
  key  <alt d>         fsort 'd'               // sort by date/time
  key  <alt o>         fsort 'o'               // no sort (Dos order)

  // Print
  key  <ctrl p>        print                   // print fmgr contents


//--------------------------------------------------------------------
// Movable/Sizable windows
//--------------------------------------------------------------------

object win

  key  <ctrl f5>       sizekey                 // move/size with kbd
  key  <ctrl f6>       pankey                  // pan video with kbd

  key  <alt ins>       sizewin -2 -1 -2 -1 2   // move window northwest
  key  <alt del>       sizewin  2  1  2  1 2   // move window southeast


//--------------------------------------------------------------------
// All windows
//--------------------------------------------------------------------

object mon

  // key macros
  key  <ctrl r>        record                  // toggle record setting
  key  <ctrl e>        play                    // play scrap key macro

  // define multi-key prefixes
  key  <ctrl k>        prefix <ctrl k>         // define <ctrl k> prefix
  key  <ctrl o>        prefix <ctrl o>         // define <ctrl o> prefix
  key  <ctrl q>        prefix <ctrl q>         // define <ctrl q> prefix

